<?xml version="1.0" encoding="utf-8"?>
<Canvas xmlns="http://www.adobe.com/2006/mxml" xmlns:local="*" xmlns:qs.validators="qs.validators.*" xmlns:sjdControls="sjd.controls.*"
	borderStyle="solid" cornerRadius="10" borderColor="#888888"
	backgroundColor="#565656" backgroundAlpha="0.75" width="546" height="292"  
	creationComplete="init();"
	defaultButton="{okButton}" >
	
	<Script>
		<![CDATA[
			import mx.events.ValidationResultEvent;
			import mx.events.CloseEvent;
			import mx.collections.ArrayCollection;
			import qs.controls.CalendarDisplay;
			import mx.events.FlexEvent;
			import qs.utils.DateRange;
			import mx.core.Application;
			import qs.utils.StringUtils;
			import qs.calendar.CalendarEvent;
			import qs.utils.DateUtils;
			import mx.controls.Alert;
			import mx.controls.RadioButtonGroup;
			import mx.managers.PopUpManager;
			
			import mx.logging.*;
			
			
			public var event:CalendarEvent;
			
			[Bindable]
			public var color:uint;
			[Bindable]
			public var startDate:Date;
			[Bindable]
			public var endDate:Date;
			[Bindable]
			public var eventName:String;
			[Bindable]
			public var eventDesc:String;
			
			public var cd:CalendarDisplay = null;
			
			private var myLogger:ILogger = Log.getLogger("EditCalendarEventDialog");

			
			public function init():void{
				color = event.color;
				startDate = event.start;
				endDate = event.end;
				eventName = event.summary;
				eventDesc = StringUtils.replaceAll(event.description, '\\n', '\n')
			}
			
			public function cancel():void
			{
				PopUpManager.removePopUp(this);		
			}
			
			public function edit():void
			{
				doValid();
				
				if(okButton.enabled){
					
					if(startDateUI.text == endDateUI.text){
						//Edit Time
						event.allDay = false;
						
						event.start = startDateUI.selectedDate;
						event.start.hours = int(timeUI.values[0]);
						if(timeUI.values[0] - int(timeUI.values[0]) > 0){
							event.start.minutes = 30;
						}else{
							event.start.minutes = 0;
						}
						event.end = endDateUI.selectedDate;
						event.end.hours = int(timeUI.values[1]);
						if(timeUI.values[1] - int(timeUI.values[1]) > 0){
							event.end.minutes = 30;
						}else{
							event.end.minutes = 0;
						}
						
						event.range = new DateRange(event.start, event.end);
						
					}else{
						//Add Days
						if(startDateUI.selectedDate.getTime() >= endDateUI.selectedDate.getTime()){
							Alert.show("Start Date must be earlier than the End Date!");
							return;
						}
						
						event.allDay = true;
						
						if(event.start == startDateUI.selectedDate && event.end == endDateUI.selectedDate){
							//No changed
						}else{
							if(event.start != startDateUI.selectedDate){
								event.start = startDateUI.selectedDate;
							}
							if(event.end != endDateUI.selectedDate){
								event.end = endDateUI.selectedDate;
								event.end.date += 1;
								event.end.milliseconds -= 1;
							}
							event.range = new DateRange(event.start, event.end);
						}
						
						
					}
					
					event.summary = StringUtils.trim(eventNameUI.text);
					var desc:String = eventDescUI.text;
					desc = StringUtils.replaceAll(desc, "\n", "\\n");
					desc = StringUtils.replaceAll(desc, "\r", "\\n");
					event.description = desc;
					event.color = colorUI.selectedColor;
					
					PopUpManager.removePopUp(this);
					
					var cdac:ArrayCollection = cd.dataProvider as ArrayCollection;
					var index:Number = cdac.getItemIndex(event);
					cdac.removeItemAt(index);
					callLater(refreshEvent, [index, cdac]);
				}
			}
			
			private function refreshEvent(index:Number, ac:ArrayCollection):void{
				ac.addItemAt(event, index);
			}
			
			private function dateChangeHandle():void{
				timeUI.values = [0, 23.5];
				timeValuesUI.text = getTimeSliderDataTip(timeUI.values[0]) + " - " + getTimeSliderDataTip(timeUI.values[1]);
				if(startDateUI.text == endDateUI.text){
					timeUI.enabled = true;
				}else{
					timeUI.enabled = false;
				}
			}
			
			private function getTimeSliderDataTip(value:Number):String {
				return (value - int(value) > 0) ? String(value).replace(".5", ":30") : value + ":00";
			}
			
			public function refresh():void{
				timeValuesUI.text = getTimeSliderDataTip(timeUI.values[0]) + ' - ' + getTimeSliderDataTip(timeUI.values[1])
				var enable:Boolean = (startDateUI.text == endDateUI.text);
				timeUI.enabled = enable
				
				eventValidator.enabled=true;
			}
			
			private function del():void{
				Alert.show("Do you want to delete the event?\n\n" + event.summary + "\n" + startDateUI.text +" "+ getTimeSliderDataTip(timeUI.values[0]) +" - "+ endDateUI.text +" "+ getTimeSliderDataTip(timeUI.values[1]), "Delete Event", 3, this, alertClickHandler);
			}
			
			private function alertClickHandler(e:CloseEvent):void {
                if (e.detail == Alert.YES){
                    var cdac:ArrayCollection = cd.dataProvider as ArrayCollection;
					var index:Number = cdac.getItemIndex(event);
					cdac.removeItemAt(index);
					PopUpManager.removePopUp(this);
                }else{
                }
            }

			private function handleValid(eventObj:ValidationResultEvent):void {
				if(okButton){
	                if(eventObj.type == ValidationResultEvent.VALID){
	                    // Enable Submit button.
	                    okButton.enabled = true;
	                }else{
	                    okButton.enabled = false;
	                }
                }
            }
            
            private function doValid():void{
            	if(eventModel){
            		eventValidator.validate();
            	}
            }


		]]>
	</Script>
	
	<Binding source="colorUI.selectedColor" destination="color" />
	<Binding source="startDateUI.selectedDate" destination="startDate" />
	<Binding source="endDateUI.selectedDate" destination="endDate" />
	<Binding source="eventNameUI.text" destination="eventName" />
	<Binding source="eventDescUI.text" destination="eventDesc" />
		
	<Form left="10" y="10" right="10" bottom="20" >
		<FormItem label="Start Date:" width="100%">
			<DateField id="startDateUI" color="#000000" selectedDate="{startDate}" change="dateChangeHandle()" valueCommit="doValid();" />
		</FormItem>
		<FormItem label="End Date:" width="100%">
			<DateField id="endDateUI" color="#000000" selectedDate="{endDate}" change="dateChangeHandle()" valueCommit="doValid();" />
		</FormItem>
		<FormItem label="Time:" width="100%">
			<HBox width="100%">
				<Text id="timeValuesUI" width="70" text="" />
				<HSlider width="100%" id="timeUI" change="refresh();" dataTipFormatFunction="getTimeSliderDataTip" liveDragging="true" minimum="0" maximum="23.5" showDataTip="true" snapInterval="0.5" thumbCount="2" values="[0, 23.5]"/>
			</HBox>
		</FormItem>
		<FormItem label="Event Name:" width="100%">
			<sjdControls:ClearTextInput id="eventNameUI" width="100%" defultText="Please input event name..." defultTextColor="#FF0000" color="#000000" text="{eventName}" valueCommit="doValid();"/>
		</FormItem>
		<FormItem label="Event Description:" width="100%">
			<TextArea id="eventDescUI" width="100%" height="50" color="#000000" text="{eventDesc}"/>
		</FormItem>
			
		<FormItem label="Event Color:">
			<ColorPicker id="colorUI" width="44" selectedColor="#BB0000"/>  <!--selectedColor="{color}"/>-->
		</FormItem>
	</Form>
	<Button id="delButton" x="12" bottom="10" label="Delete" color="#400040" click="del();"/>
	<Button id="okButton" x="434" bottom="10" label="Edit" color="#400040" click="edit();"/>
	<Button id="cancelButton" x="361" bottom="10" label="Cancel" color="#000000" click="cancel();" />
	<Label x="10" y="0" text="Edit a new Days/Time Event" styleName="CreateCalendarDialogHeader"/>
	
	<Model id="eventModel">
		<root>
			<event>
				<name>{eventNameUI.text}</name>
				<date>
					<startDate>{startDateUI.selectedDate.getTime()}</startDate>
					<endDate>{endDateUI.selectedDate.getTime()}</endDate>
				</date>
			</event>
		</root>
	</Model>
    
	<qs.validators:EventValidator id="eventValidator" enabled="false" allListeners="{[eventNameUI, startDateUI, endDateUI]}" source="{eventModel}" property="event" required="false" valid="handleValid(event)" invalid="handleValid(event)" />
	
	
</Canvas>